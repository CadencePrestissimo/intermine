buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven {
            url "https://oss.jfrog.org/artifactory/oss-snapshot-local"
        }
    }
    dependencies {
        classpath group: 'org.intermine', name: 'plugin', version: System.getProperty("imVersion")
    }
}

apply plugin: 'dbmodel'
apply plugin: 'biocore-dbmodel'

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
        resources {
            srcDirs = ['src/test/resources']
        }
    }
}

processResources {
    from('.') { include ("core.xml", "genomic_additions.xml")}
}

configurations {
    provided
    compile.extendsFrom provided
    bioModel
}

dependencies {
    compile group: "org.intermine", name: "intermine-resources", version: System.getProperty("imVersion")
    compile group: "org.intermine", name: "intermine-objectstore", version: System.getProperty("imVersion") // idresolver needs db
    compile group: "org.intermine", name: "intermine-api", version: System.getProperty("imVersion")
    provided fileTree(dir: 'libs', include: '*.jar') //biojava-1.7.1.jar not found in public repo, obo and bbop not available in the public maven repo
    testCompile group: "org.intermine", name: "intermine-integrate", version: System.getProperty("imVersion"), classifier: "testClasses"
    testCompile group: "org.intermine", name: "bio-source-flymine-static", version: System.getProperty("imVersion")
    bioModel group: "org.intermine", name: "bio-model", version: System.getProperty("imVersion"), transitive: false
    runtime fileTree(dir: 'libs', include: '*.jar') //antlr-2.7.6-caching.jar
}

dbModelConfig {
    objectStoreName = "os.bio-test"
    modelName = "genomic"
    defaultInterminePropertiesFile = "default.intermine.bio-test.properties"
}

task copyMineProperties(type: Copy) {
    from "${System.env.HOME}/.intermine/intermine-bio-test.properties"
    into sourceSets.main.output.resourcesDir
    rename { fileName -> fileName.replace('intermine-bio-test.properties', 'intermine.properties') }
    inputs.sourceFiles.stopExecutionIfEmpty()
}

task copyGenomicModel(type: Copy, overwrite: true) {
    from zipTree(configurations.bioModel.singleFile)
    into sourceSets.main.output.resourcesDir
}


tasks.test.dependsOn 'buildDB'
tasks.buildDB.dependsOn ':bio-testresources:createUnitTestDatabases'
tasks.mergeModels.dependsOn 'processResources', 'copyGenomicModel', 'copyMineProperties', 'copyDefaultInterMineProperties'

jar {
    dependsOn configurations.runtime
    // put all the JARs included in `provided` config and include them in the JAR
    from {
        (configurations.provided).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}
